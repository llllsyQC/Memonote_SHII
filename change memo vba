Sub 工数集計_自動処理()

    Dim 年 As String, 月 As String, yyyymm As String
    Dim pathDaily As String, pathSave As String
    Dim fileName1 As String, fileName2 As String, fileName3 As String
    Dim wb1 As Workbook, wb2 As Workbook, wb3 As Workbook
    Dim wsMain As Workbook
    Dim wsSrc As Worksheet, wsDest As Worksheet
    Dim 工数map As Object
    Dim srcRow As Long, destRow As Long, cc As Long
    Dim 工数番号 As Variant

    pathDaily = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\06.日報\06.50定時_工数集計\"
    pathSave = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\14.定例会\編集用資料\【原紙】\集計シート\作成済\"

    年 = InputBox("【年】を入力（例：2025）")
    月 = InputBox("【月】を入力（例：04）")
    If 年 = "" Or 月 = "" Then MsgBox "年と月の入力が必要です。": Exit Sub
    yyyymm = 年 & 月

    Set wsMain = ThisWorkbook

    '①定時作業チェック
    fileName1 = "工数集計_定時作業チェックシート_" & yyyymm & ".xlsx"
    If Dir(pathDaily & fileName1) <> "" Then
        Set wb1 = Workbooks.Open(pathDaily & fileName1)
        With wb1.Worksheets(1)
            .Columns.Hidden = False: .Rows.Hidden = False
        End With
        wb1.Worksheets(1).Range("M4:A49").Copy
        wsMain.Worksheets("定時チェック").Range("B3").PasteSpecial xlPasteValues
        wb1.Worksheets(1).Range("M51:A96").Copy
        wsMain.Worksheets("定時チェック").Range("B50").PasteSpecial xlPasteValues
        wb1.Close False
    End If

    '②定時外
    fileName2 = "工数集計_定時外_" & yyyymm & ".xlsx"
    If Dir(pathDaily & fileName2) <> "" Then
        Set wb2 = Workbooks.Open(pathDaily & fileName2)
        wb2.Worksheets(1).Range("F4:B50").Copy
        wsMain.Worksheets("定時外").Range("F7").PasteSpecial xlPasteValues
        wb2.Close False
    End If

    '④日報
    fileName3 = "工数集計_日報_" & yyyymm & ".xlsx"
    If Dir(pathDaily & fileName3) <> "" Then
        Set wb3 = Workbooks.Open(pathDaily & fileName3)
        wb3.Worksheets(1).Range("F4:LC53").Copy
        wsMain.Worksheets("日報").Range("F7").PasteSpecial xlPasteValues
        wb3.Close False
    End If

    ' 年月写入
    With wsMain.Worksheets("工数取得-都度対応項目(時間)")
        .Range("I5").Value = 年
        .Range("I8").Value = 月
    End With

    ' 0消しから工数取得への転送（工数番号対応）
    Set 工数map = CreateObject("Scripting.Dictionary")
    Set wsDest = wsMain.Worksheets("工数取得-都度対応項目(時間)")
    For destRow = 2 To 100
        工数番号 = wsDest.Cells(destRow, "E").Value
        If 工数番号 <> "" Then 工数map(工数番号) = destRow
    Next
    Set wsSrc = wsMain.Worksheets("0消し(最終)")
    For srcRow = 2 To 100
        工数番号 = wsSrc.Cells(srcRow, "E").Value
        If 工数map.exists(工数番号) Then
            destRow = 工数map(工数番号)
            For cc = 6 To 26
                With wsDest.Cells(destRow, cc)
                    .NumberFormat = "h:mm"
                    .Value = wsSrc.Cells(srcRow, cc).Value
                End With
            Next cc
        End If
    Next

    ' 月次保存
    Application.DisplayAlerts = False
    wsMain.SaveAs Filename:=pathSave & "月次工数集計シート" & yyyymm & ".xlsm", FileFormat:=xlOpenXMLWorkbookMacroEnabled
    Application.DisplayAlerts = True

    MsgBox "月次工数集計シートを保存しました：" & yyyymm
    Call CopyTo_Ver52(yyyymm, 年, 月)

End Sub

Sub CopyTo_Ver52(yyyymm As String, 年 As String, 月 As String)

    Dim pathVer52 As String, pathTemplateSheet As String, pathSave As String
    Dim fileNameTemplate As String
    Dim wbTarget As Workbook, wbSource As Workbook
    Dim wsTarget As Worksheet, wsSource As Worksheet
    Dim rngSource As Range, rngTarget As Range
    Dim r As Long, c As Long
    Dim answer As VbMsgBoxResult, saveName As String

    pathVer52 = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\14.定例会\編集用資料\【原紙】\IDCF工数提出用マクロファイル\"
    pathTemplateSheet = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\14.定例会\編集用資料\【原紙】\集計シート\"
    pathSave = pathTemplateSheet & "作成済\"

    Set wbTarget = Workbooks.Open(pathVer52 & "工数取得ver.5.2（大宮）.xlsm")
    Set wsTarget = wbTarget.Worksheets("都度対応項目(時間)")
    wsTarget.Range("I6").Value = 年
    wsTarget.Range("I7").Value = 月

    fileNameTemplate = Dir(pathTemplateSheet & "月次工数集計シート【原紙】" & yyyymm & "*.xlsm")
    If fileNameTemplate = "" Then MsgBox "原紙ファイルが見つかりません": Exit Sub
    Set wbSource = Workbooks.Open(pathTemplateSheet & fileNameTemplate)
    Set wsSource = wbSource.Worksheets("工数取得-都度対応項目(時間)")

    Set rngSource = wsSource.Range("O11:EH64")
    Set rngTarget = wsTarget.Range("O13:EH63")

    For r = 1 To rngSource.Rows.Count
        For c = 1 To rngSource.Columns.Count
            With rngTarget.Cells(r, c)
                .NumberFormat = "h:mm"
                .Value = rngSource.Cells(r, c).Value
            End With
        Next c
    Next r

    wbSource.Close False

    answer = MsgBox("OJT社員がすでに定時外工数へ記録済みですか？" & vbCrLf & _
                    "Yes：自動処理をスキップします。" & vbCrLf & "No：自動処理を続けます。", vbYesNo + vbQuestion)

    If answer = vbNo Then Call FillAdditionalData(yyyymm, 年, 月) _
    Else MsgBox "OJT対応：手動で勤務時間・稼働率を調整してください", vbInformation

    saveName = "工数取得ver.5.2（大宮）" & Mid(yyyymm, 3, 4) & ".xlsm"
    Application.DisplayAlerts = False
    wbTarget.SaveAs pathSave & saveName, xlOpenXMLWorkbookMacroEnabled
    Application.DisplayAlerts = True

    MsgBox "Ver5.2 ファイル保存完了：" & saveName

End Sub

Sub FillAdditionalData(yyyymm As String, 年 As String, 月 As String)

    Dim pathShift As String, pathZangyo As String
    Dim fileShift As String
    Dim wbTarget As Workbook, wsTarget As Worksheet
    Dim wbShift As Workbook, wbZangyo As Workbook
    Dim cell As Range
    Dim totalEffort As Double, totalTime As Double, rate As Double
    Dim msgText As String

    pathShift = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\05.SBていsy物\シフト表\99.大宮勤怠作成自動化ファイル\"
    pathZangyo = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\05.SBていsy物\シフト表\"

    Set wbTarget = ActiveWorkbook
    Set wsTarget = wbTarget.Worksheets("都度対応項目(時間)")

    fileShift = Dir(pathShift & "【大宮】" & 年 & "年" & 月 & "月度勤務割表*.xlsm")
    If fileShift <> "" Then
        Set wbShift = Workbooks.Open(pathShift & fileShift)
        With wbShift.Sheets(1)
            For Each cell In .UsedRange
                If cell.Value = "総勤務時間" Then wsTarget.Range("H10").NumberFormat = "h:mm": wsTarget.Range("H10").Value = cell.Offset(0, 1).Value
                If cell.Value = "総平日日勤時間" Then wsTarget.Range("H18").NumberFormat = "h:mm": wsTarget.Range("H18").Value = cell.Offset(0, 1).Value
            Next
        End With
        wbShift.Close False
    End If

    If Dir(pathZangyo & "残業時間管理表" & 年 & "年度.xlsx") <> "" Then
        Set wbZangyo = Workbooks.Open(pathZangyo & "残業時間管理表" & 年 & "年度.xlsx")
        With wbZangyo.Sheets(1)
            wsTarget.Range("H14").NumberFormat = "h:mm"
            wsTarget.Range("H14").Value = .Range("AL4").Value
            wsTarget.Range("H22").NumberFormat = "h:mm"
            wsTarget.Range("H22").Value = .Range("AO4").Value
        End With
        wbZangyo.Close False
    End If

    totalTime = GetMinutes(wsTarget.Range("H10").Value)
    totalEffort = GetMinutes(Application.WorksheetFunction.Sum(wsTarget.Range("EP11:EP64")))
    If totalTime > 0 Then rate = totalEffort / totalTime Else rate = 0

    msgText = "稼働率 = " & Format(rate, "0.0%") & vbCrLf & _
              "工数合計：" & Format(totalEffort / 60, "0.0") & " 時間 ／ 勤務時間：" & Format(totalTime / 60, "0.0") & " 時間"

    MsgBox msgText, vbInformation, "稼働率確認"

End Sub

Function GetMinutes(timeVal As Variant) As Double
    If IsDate(timeVal) Then GetMinutes = CDbl(timeVal) * 24 * 60 Else GetMinutes = 0
End Function