Sub 工数集計_自動処理()

    Dim 年 As String, 月 As String, yyyymm As String

    年 = InputBox("【年】を入力してください（例：2025）")
    月 = InputBox("【月】を入力してください（例：04）")

    If 年 = "" Or 月 = "" Then
        MsgBox "年と月の入力が必要です。": Exit Sub
    End If

    yyyymm = 年 & 月

    Call Import_AllSources(yyyymm, 年, 月)
    Call Copy_WorkHours_By工数番号()
    Call Save_As_月次集計ファイル(yyyymm)
    Call Process_Ver52(yyyymm, 年, 月)

End Sub


Sub Import_AllSources(yyyymm As String, 年 As String, 月 As String)

    Dim pathDaily As String
    pathDaily = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\06.日報\06.50定時_工数集計\"

    Dim fileName1 As String, fileName2 As String, fileName4 As String
    Dim wb1 As Workbook, wb2 As Workbook, wb4 As Workbook

    ' ① 定時作業チェックシート
    fileName1 = Dir(pathDaily & "①工数集計_定時作業チェックシート_*_" & yyyymm & ".xlsx")
    If fileName1 <> "" Then
        Set wb1 = Workbooks.Open(pathDaily & fileName1)
        With wb1.Worksheets("定時作業工数集計")
            .Columns.Hidden = False
            .Rows.Hidden = False
        End With
        ActiveWindow.FreezePanes = False
        wb1.Worksheets("定時作業工数集計").Range("M4:AQ49").Copy
        ThisWorkbook.Worksheets("定（日）").Range("B3").PasteSpecial xlPasteValues
        wb1.Worksheets("定時作業工数集計").Range("M51:AQ96").Copy
        ThisWorkbook.Worksheets("定（夜）").Range("B3").PasteSpecial xlPasteValues
        wb1.Close False
    End If

    ' ② 定時外
    fileName2 = Dir(pathDaily & "②工数集計_定時外_*_" & yyyymm & ".xlsx")
    If fileName2 <> "" Then
        Set wb2 = Workbooks.Open(pathDaily & fileName2)
        wb2.Worksheets("0集計シート").Range("B4:F50").Copy
        ThisWorkbook.Worksheets("定時外").Range("F7").PasteSpecial xlPasteValues
        wb2.Close False
    End If

    ' ④ 日報
    fileName4 = Dir(pathDaily & "④工数集計_日報_*_" & yyyymm & ".xlsx")
    If fileName4 <> "" Then
        Set wb4 = Workbooks.Open(pathDaily & fileName4)
        wb4.Worksheets("日報").Range("F7:LC53").Copy
        ThisWorkbook.Worksheets("日報").Range("F7").PasteSpecial xlPasteValues
        wb4.Close False
    End If

    ' 年月を工数取得シートに記入
    With ThisWorkbook.Worksheets("工数取得-都度対応項目(時間)")
        .Range("I5").Value = 年
        .Range("I8").Value = 月
    End With

End Sub


Sub Copy_WorkHours_By工数番号()

    Dim wsSrc As Worksheet, wsDest As Worksheet
    Dim 工数Map As Object
    Dim srcRow As Long, destRow As Long, cc As Long
    Dim 工数番号 As Variant

    Set wsSrc = ThisWorkbook.Worksheets("0消し(最終)")
    Set wsDest = ThisWorkbook.Worksheets("工数取得-都度対応項目(時間)")
    Set 工数Map = CreateObject("Scripting.Dictionary")

    For destRow = 2 To 100
        工数番号 = wsDest.Cells(destRow, "E").Value
        If 工数番号 <> "" Then 工数Map(工数番号) = destRow
    Next

    For srcRow = 2 To 100
        工数番号 = wsSrc.Cells(srcRow, "E").Value
        If 工数Map.exists(工数番号) Then
            destRow = 工数Map(工数番号)
            For cc = 6 To 26
                With wsDest.Cells(destRow, cc)
                    .NumberFormat = "h:mm"
                    .Value = wsSrc.Cells(srcRow, cc).Value
                End With
            Next cc
        End If
    Next

End Sub


Sub Save_As_月次集計ファイル(yyyymm As String)

    Dim pathSave As String
    pathSave = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\14.定例会\編集用資料\【原紙】\集計シート\作成済\"

    Application.DisplayAlerts = False
    ThisWorkbook.SaveAs Filename:=pathSave & "月次工数集計シート" & yyyymm & ".xlsm", FileFormat:=xlOpenXMLWorkbookMacroEnabled
    Application.DisplayAlerts = True

    MsgBox "月次工数集計シートを保存しました：" & yyyymm

End Sub


Sub Process_Ver52(yyyymm As String, 年 As String, 月 As String)

    Dim wbVer52 As Workbook, wbSource As Workbook
    Dim wsTarget As Worksheet, wsSource As Worksheet
    Dim pathVer52 As String, pathTemplate As String, pathSave As String
    Dim fileNameSource As String, saveName As String
    Dim answer As VbMsgBoxResult
    Dim r As Long, c As Long

    pathVer52 = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\14.定例会\編集用資料\【原紙】\IDCF工数提出用マクロファイル\"
    pathTemplate = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\14.定例会\編集用資料\【原紙】\集計シート\"
    pathSave = pathTemplate & "作成済\"

    Set wbVer52 = Workbooks.Open(pathVer52 & "工数取得ver.5.2（大宮）.xlsm")
    Set wsTarget = wbVer52.Worksheets("都度対応項目(時間)")
    wsTarget.Range("I6").Value = 年
    wsTarget.Range("I7").Value = 月

    fileNameSource = Dir(pathTemplate & "月次工数集計シート【原紙}" & yyyymm & "*.xlsm")
    If fileNameSource = "" Then
        MsgBox "原紙ファイルが見つかりません": Exit Sub
    End If
    Set wbSource = Workbooks.Open(pathTemplate & fileNameSource)
    Set wsSource = wbSource.Worksheets("工数取得-都度対応項目(時間)")

    For r = 1 To 54
        For c = 15 To 138  ' O〜EH
            With wsTarget.Cells(r + 12, c)
                .NumberFormat = "h:mm"
                .Value = wsSource.Cells(r + 10, c).Value
            End With
        Next c
    Next r

    wbSource.Close False

    answer = MsgBox("OJT社員が定時外工数へ入力済みですか？", vbYesNo + vbQuestion)

    If answer = vbNo Then
        Call FillAdditionalData(yyyymm, 年)
    Else
        MsgBox "OJT工数は手動で確認してください。"
    End If

    saveName = "工数取得ver.5.2（大宮）" & Mid(yyyymm, 3, 4) & ".xlsm"
    Application.DisplayAlerts = False
    wbVer52.SaveAs pathSave & saveName, xlOpenXMLWorkbookMacroEnabled
    Application.DisplayAlerts = True

    MsgBox "Ver5.2 ファイル保存完了：" & saveName

End Sub


Sub FillAdditionalData(yyyymm As String, 年 As String)

    Dim wbTarget As Workbook, wsTarget As Worksheet
    Dim wbShift As Workbook, wbZangyo As Workbook
    Dim pathShift As String, pathZangyo As String
    Dim cell As Range
    Dim totalEffort As Double, totalTime As Double, rate As Double
    Dim msgText As String

    pathShift = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\05.SBていsy物\シフト表\99.大宮勤怠作成自動化ファイル\"
    pathZangyo = "\\bbwcfs.local\share4\SBM1\SharePrj\大宮データセンター運用フォルダ\05.SBていsy物\シフト表\"

    Set wbTarget = ActiveWorkbook
    Set wsTarget = wbTarget.Worksheets("都度対応項目(時間)")

    Dim fileShift As String
    fileShift = Dir(pathShift & "【大宮】" & 年 & "年" & Right(yyyymm, 2) & "月度勤務割表*.xlsm")
    If fileShift <> "" Then
        Set wbShift = Workbooks.Open(pathShift & fileShift)
        For Each cell In wbShift.Sheets(1).UsedRange
            If cell.Value = "総勤務時間" Then wsTarget.Range("H10").Value = cell.Offset(0, 1).Value
            If cell.Value = "総平日日勤時間" Then wsTarget.Range("H18").Value = cell.Offset(0, 1).Value
        Next
        wbShift.Close False
    End If

    If Dir(pathZangyo & "残業時間管理表" & 年 & "年度.xlsx") <> "" Then
        Set wbZangyo = Workbooks.Open(pathZangyo & "残業時間管理表" & 年 & "年度.xlsx")
        wsTarget.Range("H14").Value = wbZangyo.Sheets(1).Range("AL4").Value
        wsTarget.Range("H22").Value = wbZangyo.Sheets(1).Range("AO4").Value
        wbZangyo.Close False
    End If

    totalEffort = GetMinutes(Application.WorksheetFunction.Sum(wsTarget.Range("EP11:EP64")))
    totalTime = GetMinutes(wsTarget.Range("H10").Value)
    If totalTime > 0 Then rate = totalEffort / totalTime Else rate = 0

    msgText = "稼働率 = " & Format(rate, "0.0%") & vbCrLf & _
              "（工数合計：" & Format(totalEffort / 60, "0.0") & "時間 ／ 勤務時間：" & Format(totalTime / 60, "0.0") & "時間）"

    MsgBox msgText, vbInformation, "稼働率確認"

End Sub


Function GetMinutes(timeVal As Variant) As Double
    If IsDate(timeVal) Then
        GetMinutes = CDbl(timeVal) * 24 * 60
    Else
        GetMinutes = 0
    End If
End Function